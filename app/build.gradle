apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.triplet.play'

def keystorePropertiesFile = rootProject.file("C:\\android/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def playPropertiesFile = rootProject.file("C:\\android/play.properties")
def playProperties = new Properties()
playProperties.load(new FileInputStream(playPropertiesFile))

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "com.coreywjohnson.setlists"
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 11
        versionName "0.4.4 Alpha"
    }
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
}

play {
    serviceAccountEmail = playProperties['playEmail']
    pk12File = file(playProperties['playKey'])
    track = playProperties['playTrack']
}

ext {
    supportLibraryVersion = '24.2.0'
    firebaseVersion = '9.2.0'
    daggerVersion = '2.2'
    retrofitVersion = '2.0.2'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:design:${supportLibraryVersion}"
    compile "com.android.support:cardview-v7:${supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile("com.squareup.retrofit2:converter-simplexml:${retrofitVersion}") {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    compile "com.google.code.gson:gson:2.6.2"
    compile "org.glassfish:javax.annotation:10.0-b28"
    compile "com.google.dagger:dagger:${daggerVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"
    compile "com.google.firebase:firebase-core:${firebaseVersion}"
    compile "com.google.firebase:firebase-crash:${firebaseVersion}"
    compile 'com.squareup.picasso:picasso:2.5.2'
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.10.19"
}

apply plugin: 'com.google.gms.google-services'
